apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'org.jetbrains.dokka'

dokkaHtml.configure {
    dokkaSourceSets {
        named("main") {
            noAndroidSdkLink.set(false)
        }
    }
}

task androidJavadocsJar(type: Jar, dependsOn: dokkaHtml) {
    description 'Generates a JavaDoc archive (.jar) from the JavaDoc files'
    from "$buildDir/dokka"
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
    from android.sourceSets.main.kotlin.srcDirs
}

artifacts {
    archives androidSourcesJar, androidJavadocsJar
}

afterEvaluate{
    publishing {
        publications {
            release(MavenPublication)  {
                groupId 'ch.sbb.mobile.ml'
                version PUBLISH_VERSION
                artifactId 'sbbml'

                from components.release

                artifact androidSourcesJar
                artifact androidJavadocsJar

                pom {
                    name = 'sbbml'
                    description = 'SBB ML Library'
                    url = 'https://github.com/SchweizerischeBundesbahnen/mobile-android-ml'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://github.com/SchweizerischeBundesbahnen/mobile-android-ml/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            name = 'Henrik Karppinen'
                            email = 'mobile@sbb.ch'
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/SchweizerischeBundesbahnen/mobile-android-ml.git'
                        developerConnection = 'scm:git:ssh://github.com/SchweizerischeBundesbahnen/mobile-android-ml.git'
                        url = 'https://github.com/SchweizerischeBundesbahnen/mobile-android-ml/tree/main'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}

dependencies {
    dokkaHtmlPlugin "org.jetbrains.dokka:kotlin-as-java-plugin:$dokka_version"
}
